plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id("org.cadixdev.licenser") version "0.5.0"
}

group 'org.quiltmc'
version '0.3.0'

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
    mavenCentral()
    maven {
        name = 'Quilt'
        url = 'https://maven.quiltmc.org/repository/release/'
    }
}

dependencies {
    implementation 'com.squareup:javapoet:1.13.0'
    implementation 'org.quiltmc:tiny-mappings-parser:0.3.0'
    runtimeOnly 'com.google.guava:guava:30.1.1-jre'

    implementation 'org.ow2.asm:asm:9.1'
    implementation 'org.ow2.asm:asm-analysis:9.1'
    implementation 'org.ow2.asm:asm-commons:9.1'
    implementation 'org.ow2.asm:asm-tree:9.1'
    implementation 'org.ow2.asm:asm-util:9.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

java {
    withSourcesJar()
}

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16
}

test {
    useJUnitPlatform()
}

tasks.withType(Jar) {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'MappingPoet',
                'Implementation-Version': archiveVersion,
                'Main-Class': "org.quiltmc.mappingpoet.Main"
    }
}

license {
    header file("HEADER")
    include '**/*.java'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}
